buildscript {
  ext {
    vavrVersion = "0.9.2"
    javaVersion = "1.8"
    axonVersion = "3.1.2"
    springBootVersion = "2.0.0.RELEASE"
    kotlinVersion = "1.2.21"
  }
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
  }
}

plugins {
  id "idea"
  id "eclipse"
  id "com.avast.gradle.docker-compose" version "0.6.13"
}

allprojects {

  apply plugin: "base"
  apply plugin: "maven"

  group = "daggerok"
  version = "0.0.1"

  defaultTasks "clean", "build"

  task wrapper(type: Wrapper) {
    gradleVersion = "4.10.2"
    distributionType = 'BIN'
  }
}

subprojects {

  apply plugin: "java"
  apply plugin: "io.spring.dependency-management"

  sourceCompatibility = targetCompatibility = "$javaVersion"

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
  }

  dependencies {
    compile("io.vavr:vavr:$vavrVersion")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion")
    compile("com.fasterxml.jackson.module:jackson-module-kotlin")
    compileOnly("org.projectlombok:lombok")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("io.projectreactor:reactor-test")
  }

  apply plugin: "kotlin"

  compileKotlin {
    kotlinOptions {
      jvmTarget = "$javaVersion"
    }
  }

  compileTestKotlin {
    kotlinOptions {
      jvmTarget = "$javaVersion"
    }
  }
}

dockerCompose {
  useComposeFiles = [project.file("docker-compose-gradle.yaml")]
  // captureContainersOutput = true
  captureContainersOutput = false
  stopContainers = true
  removeContainers = true
  removeImages = "Local"
  removeVolumes = true
  removeOrphans = true
  forceRecreate = true
  waitForTcpPorts = false
  projectName = project.name
}

composeUp.dependsOn assemble
